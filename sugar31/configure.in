#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT([SUGAR], [3.1], [dbindel@cs.berkeley.edu])
AC_CONFIG_AUX_DIR([config])
AC_CONFIG_SRCDIR([sugar_init.m])
AC_CONFIG_HEADER([config.h])

# Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_F77
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PROG_RANLIB

AC_ARG_WITH(fltk,
 AC_HELP_STRING([--with-fltk], [build with FLTK support (default no)]),
 [ac_with_fltk="$with_fltk"],
 [ac_with_fltk="no"])
if test x"$ac_with_fltk" != "xno"; then
  AC_PATH_PROG([FLTK_CONFIG], [fltk-config])
  if test x"$FLTK_CONFIG" != "x"; then
    AC_DEFINE(HAVE_FLTK, 1,
              [Define if FLTK is available]) 
    FLTK_CXXFLAGS="`$FLTK_CONFIG --cxxflags`"
    FLTK_LDFLAGS="`$FLTK_CONFIG --ldflags`"
    SUGAR_FLTK="sugar-fltk"
  fi
fi
AC_SUBST(FLTK_CONFIG)
AC_SUBST(FLTK_CXXFLAGS)
AC_SUBST(FLTK_LDFLAGS)

AC_ARG_WITH(mex,  
 AC_HELP_STRING([--with-mex=MEX_PROGRAM], [location of MEX script]),
 [if test x"$with_mex" != "xno"; then 
    MEX="$with_mex";
  fi],
 [AC_PATH_PROGS([MEX], [mex mex.bat])
])
if test x"$MEX" != "x"; then
  SUGAR_MEX="mex"
fi
AC_SUBST(MEX)

AC_PATH_PROG(AR, ar)
ARFLAGS="rc"
AC_SUBST(AR)
AC_SUBST(ARFLAGS)

# Checks for libraries.
AC_F77_LIBRARY_LDFLAGS
AC_CHECK_LIB([m], [cos])
ACX_BLAS
ACX_LAPACK
AC_WITH_SUPERLU
ACX_LIB_READLINE

AC_PATH_XTRA
if test x"$X_LIBS" != "x"; then
  X_LIBS_ALL="$X_PRE_LIBS $X_LIBS -lXaw -lXt -lX11 $X_EXTRA_LIBS"
fi
AC_SUBST(X_LIBS_ALL)

# Checks for header files.
AC_FUNC_ALLOCA
AC_HEADER_STDC
AC_CHECK_HEADERS([limits.h locale.h malloc.h stddef.h stdlib.h string.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_STRUCT_TM
AC_HEADER_STDBOOL
AC_C_VOLATILE

# Checks for library functions.
AC_FUNC_ERROR_AT_LINE
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_FUNC_MEMCMP
AC_FUNC_REALLOC
AC_TYPE_SIGNAL
AC_FUNC_STAT
AC_FUNC_STRCOLL
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([floor memchr memset pow setlocale sqrt strchr strcspn strdup strerror strpbrk strrchr strstr strtoul])

AC_CANONICAL_HOST
SUGAR_HOST=$host
AC_SUBST(SUGAR_HOST)

SUGAR_DATE=`date`
AC_SUBST(SUGAR_DATE)

DEFAULT_PROGRAMS="sugar-lua $SUGAR_FLTK $SUGAR_MEX"
AC_SUBST(DEFAULT_PROGRAMS)

AC_CONFIG_FILES([make.inc
                 sugar.h])

AC_OUTPUT
